// This sets up the dependencies for the build script *itself*, not the package.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'

// Make sure the library is Java 7 compatible
targetCompatibility = '1.7'
sourceCompatibility = '1.7'

project.version = current_version
project.group = "com.lightstep.tracer"

jar {
    baseName 'java-common'
    version = current_version
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Defines the repositories need for dependencies below
repositories {
    jcenter()
    maven {
        // published beta artifacts of opentracing API
        url  "http://oss.jfrog.org/oss-snapshot-local/"
    }
}

// Dependencies to build this JAR archive
dependencies {
    compile 'io.opentracing:opentracing-api:0.30.0'
    compile 'io.opentracing:opentracing-util:0.30.0'
    compile 'io.grpc:grpc-protobuf:1.2.0'
    compile 'io.grpc:grpc-stub:1.2.0'

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:2.2.15'
    testCompile 'io.grpc:grpc-netty:1.2.0'
    testCompile 'io.netty:netty-tcnative-boringssl-static:1.1.33.Fork26'
}

tasks.matching {it instanceof Test}.all {
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId 'com.lightstep.tracer'
            artifactId 'java-common'
            version = current_version

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile'}
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'

                name 'java-common'
                description = 'The LightStep OpenTracing Tracer implementation for Java'
                url 'https://github.com/lightstep/lightstep-tracer-java'

                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'lightstep'
                        name 'LightStep'
                        email 'hello@lightstep.com'
                    }
                }
                scm {
                    connection 'https://github.com/lightstep/lightstep-tracer-java'
                    developerConnection 'https://github.com/lightstep/lightstep-tracer-java'
                    url 'https://github.com/lightstep/lightstep-tracer-java'
                }
            }
        }
    }
}

if (project.getGradle().startParameter.taskNames.any{it.contains('bintrayUpload')}) {
    gradle.taskGraph.whenReady { taskGraph ->
        taskGraph.getAllTasks().find {
            it.path == ":java-common:generatePomFileForMavenPublication"
        }.doLast {
            file("build/publications/maven/pom-default.xml").delete()
            println 'Overriding pom-file to make sure we can sync to maven central!'
            pom {
                //noinspection GroovyAssignabilityCheck
                project {
                    name 'java-common'
                    groupId 'com.lightstep.tracer'
                    artifactId 'java-common'
                    packaging 'jar'
                    description 'The LightStep OpenTracing Tracer implementation for Java'
                    url 'https://github.com/lightstep/lightstep-tracer-java'
                    version = current_version

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id 'lightstep'
                            name 'LightStep'
                            email 'hello@lightstep.com'
                        }
                    }
                    scm {
                        connection 'https://github.com/lightstep/lightstep-tracer-java'
                        developerConnection 'https://github.com/lightstep/lightstep-tracer-java'
                        url 'https://github.com/lightstep/lightstep-tracer-java'
                    }
                }
            }.writeTo("build/publications/maven/pom-default.xml")
        }
    }
}


// Bintray configuration so we can publish publically.  Requires the environment
// variables below be set.
// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = 'java-common'
        userOrg = 'lightstep'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/lightstep/lightstep-tracer-java.git'
        publicDownloadNumbers = true
        publish = true
        version {
            name = current_version
            released  = new Date()

            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_GPG_PASSPHRASE')
            }

            // See https://github.com/bintray/bintray-examples/blob/master/gradle-example/build.gradle
            mavenCentralSync {
                sync = true
                user = System.getenv('MAVEN_CENTRAL_USER_TOKEN')
                password = System.getenv('MAVEN_CENTRAL_TOKEN_PASSWORD')
                close = '1'
            }
        }
    }
}
