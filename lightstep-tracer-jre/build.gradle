// The library versions are kept in sync
def current_version = new File('../common/VERSION').text

// This sets up the dependencies for the build script *itself*, not the package.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'

// Make sure the library is Java 7 compatible
targetCompatibility = '1.7'
sourceCompatibility = '1.7'

jar {
    baseName 'lightstep-tracer-jre'
    version = current_version
}

// By default, only "src/" will be included, but we want to include the code
// from the "common" project as well -- and want it to be part of this JAR,
// not a separate artifact.
sourceSets {
    main {
        java {
            srcDirs = [ "src/main", "../common/src"]
        }
    }
}

test {
    testLogging.showStandardStreams = true
}

// Defines the repositories need for dependencies below
repositories {
    jcenter()
    maven {
        url  "http://oss.jfrog.org/oss-snapshot-local/"
    }
}

// Dependencies to build this JAR archive
dependencies {
    compile 'io.opentracing:opentracing-api:0.1.0-SNAPSHOT'
    compile 'org.apache.thrift:libthrift:0.9.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
    compile 'org.json:json:20160212'
    testCompile 'junit:junit:4.11'
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			groupId 'com.lightstep.tracer'
    		artifactId 'lightstep-tracer-jre'
    		version = current_version
		}
	}
}

// Bintray configuration so we can publish publically.  Requires the environment
// variables below be set.
// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'lightstep-tracer-jre'
        userOrg = 'lightstep'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/lightstep/lightstep-tracer-java.git'
        publicDownloadNumbers = true
        publish = true
        version {
            name = current_version
            released  = new Date()
        }
    }
}
