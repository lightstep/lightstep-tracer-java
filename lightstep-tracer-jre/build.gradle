// This sets up the dependencies for the build script *itself*, not the package.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: "com.github.johnrengelman.shadow"

// Make sure the library is Java 7 compatible
targetCompatibility = '1.7'
sourceCompatibility = '1.7'

configurations {
    benchmarkCompile
}

sourceSets {
    main {
        java {
        }
    }
    benchmark {
        compileClasspath = configurations.benchmarkCompile
    }
}

jar {
    baseName 'lightstep-tracer-jre'
    version = current_version
    from sourceSets.main.output
}

// configure shadow jar task
shadowJar {
    classifier = null
    relocate 'com.google', 'lightstep.com.google'
    relocate 'google.protobuf', 'lightstep.google.protobuf'
    relocate 'io.netty', 'lightstep.io.netty'
    relocate 'javax.annotation', 'lightstep.javax.annotation'

    dependencies {
        exclude(dependency('org.slf4j:slf4j-api:1.7.21'))
        exclude(dependency('io.opentracing::0.30.0'))
    }
}

task benchmarkJar(type: Jar) {
    baseName 'lightstep-benchmark'
    version = current_version
    from sourceSets.benchmark.output
    from { configurations.benchmarkCompile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives benchmarkJar
}

// Defines the repositories need for dependencies below
repositories {
    jcenter()
    maven {
        // published beta artifacts of opentracing API
        url  "http://oss.jfrog.org/oss-snapshot-local/"
    }
}

// Dependencies to build this JAR archive
dependencies {
    compile 'com.lightstep.tracer:java-common:0.12.5'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'io.grpc:grpc-netty:1.2.0'
    compile 'io.netty:netty-tcnative-boringssl-static:1.1.33.Fork26'
    testCompile 'junit:junit:4.11'

    shadow 'org.slf4j:slf4j-api:1.7.21'

    benchmarkCompile project(':lightstep-tracer-jre')
    benchmarkCompile 'io.opentracing:opentracing-noop:0.30.0'
    benchmarkCompile 'io.opentracing:opentracing-util:0.30.0'
    benchmarkCompile 'com.fasterxml.jackson.core:jackson-databind:2.7.4'
}

tasks.matching {it instanceof Test}.all {
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId 'com.lightstep.tracer'
            artifactId 'lightstep-tracer-jre'
            version = current_version

            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile'}
            }
        }
        shadow(MavenPublication) {
            from components.shadow
            groupId 'com.lightstep.tracer'
            artifactId 'lightstep-tracer-jre-shadow'
            version = current_version

            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile'}
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'

                name 'lightstep-tracer-jre'
                description = 'The LightStep OpenTracing Tracer implementation for Java'
                url 'https://github.com/lightstep/lightstep-tracer-java'

                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'lightstep'
                        name 'LightStep'
                        email 'hello@lightstep.com'
                    }
                }
                scm {
                    connection 'https://github.com/lightstep/lightstep-tracer-java'
                    developerConnection 'https://github.com/lightstep/lightstep-tracer-java'
                    url 'https://github.com/lightstep/lightstep-tracer-java'
                }
            }
        }
    }
}

if (project.getGradle().startParameter.taskNames.any{it.contains('bintrayUpload')}) {
    gradle.taskGraph.whenReady { taskGraph ->
        taskGraph.getAllTasks().find {
            it.path == ":lightstep-tracer-jre:generatePomFileForMavenPublication"
        }.doLast {
            file("build/publications/maven/pom-default.xml").delete()
            println 'Overriding pom-file to make sure we can sync to maven central!'
            pom {
                //noinspection GroovyAssignabilityCheck
                project {
                    name 'lightstep-tracer-jre'
                    groupId 'com.lightstep.tracer'
                    artifactId 'lightstep-tracer-jre'
                    packaging 'jar'
                    description 'The LightStep OpenTracing Tracer implementation for Java'
                    url 'https://github.com/lightstep/lightstep-tracer-java'
                    version = current_version

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id 'lightstep'
                            name 'LightStep'
                            email 'hello@lightstep.com'
                        }
                    }
                    scm {
                        connection 'https://github.com/lightstep/lightstep-tracer-java'
                        developerConnection 'https://github.com/lightstep/lightstep-tracer-java'
                        url 'https://github.com/lightstep/lightstep-tracer-java'
                    }
                }
            }.writeTo("build/publications/maven/pom-default.xml")
        }
    }
    gradle.taskGraph.whenReady { taskGraph ->
        taskGraph.getAllTasks().find {
            it.path == ":lightstep-tracer-jre:generatePomFileForShadowPublication"
        }.doLast {
            file("build/publications/shadow/pom-default.xml").delete()
            println 'Overriding pom-file to make sure we can sync to maven central!'
            pom {
                //noinspection GroovyAssignabilityCheck
                project {
                    name 'lightstep-tracer-jre-shadow'
                    groupId 'com.lightstep.tracer'
                    artifactId 'lightstep-tracer-jre-shadow'
                    packaging 'jar'
                    description 'The LightStep OpenTracing Tracer implementation for Java - Shadow Version'
                    url 'https://github.com/lightstep/lightstep-tracer-java'
                    version = current_version

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id 'lightstep'
                            name 'LightStep'
                            email 'hello@lightstep.com'
                        }
                    }
                    scm {
                        connection 'https://github.com/lightstep/lightstep-tracer-java'
                        developerConnection 'https://github.com/lightstep/lightstep-tracer-java'
                        url 'https://github.com/lightstep/lightstep-tracer-java'
                    }
                }
            }.writeTo("build/publications/shadow/pom-default.xml")
        }
    }
}


// Bintray configuration so we can publish publicly.  Requires the environment
// variables below be set.
// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['maven','shadow']
    pkg {
        repo = 'maven'
        name = 'lightstep-tracer-jre'
        userOrg = 'lightstep'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/lightstep/lightstep-tracer-java.git'
        publicDownloadNumbers = true
        publish = true
        version {
            name = current_version
            released  = new Date()

            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_GPG_PASSPHRASE')
            }

            // See https://github.com/bintray/bintray-examples/blob/master/gradle-example/build.gradle
            mavenCentralSync {
                sync = true
                user = System.getenv('MAVEN_CENTRAL_USER_TOKEN')
                password = System.getenv('MAVEN_CENTRAL_TOKEN_PASSWORD')
                close = '1'
            }
        }
    }
}
